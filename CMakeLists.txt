cmake_minimum_required(VERSION 3.13)
cmake_policy(SET CMP0077 NEW)

list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR}/cmake)
include(CMakeRC)

project(kaacore)

option(KAACORE_SHARED "Build kaacore-shared" OFF)
option(KAACORE_BUILD_DEMOS "Build kaacore demos" ON)
option(KAACORE_BUILD_DEMOS_WITH_TSAN "Build kaacore demos with thread-sanitizer" OFF)
option(KAACORE_MULTITHREADING_MODE "Build kaacore in multithreading mode" ON)

set(KAACORE_MAX_VIEWS 32 CACHE STRING "")
set(BGFX_MAX_VIEWS 64 CACHE STRING "")
if(KAACORE_MAX_VIEWS GREATER_EQUAL BGFX_MAX_VIEWS)
    message(FATAL_ERROR
        "BGFX_MAX_VIEWS(${BGFX_MAX_VIEWS}) must be greater than "
        "KAACORE_MAX_VIEWS(${KAACORE_MAX_VIEWS}).")
endif()

# force static bgfx build
set(BUILD_SHARED_LIBS OFF)
# force dynamic SDL build
set(SDL_SHARED_ENABLED_BY_DEFAULT ON)

add_subdirectory(third_party/bgfx)
target_compile_options(
    bgfx PUBLIC
    -DBGFX_CONFIG_MAX_VIEWS=${BGFX_MAX_VIEWS}
    -DBGFX_CONFIG_MULTITHREADED=$<BOOL:${KAACORE_MULTITHREADING_MODE}>)
if (TARGET astc-codec)
    set_target_properties(astc-codec PROPERTIES POSITION_INDEPENDENT_CODE ON)
endif()

set(SDL_STATIC OFF CACHE BOOL "")
add_subdirectory(third_party/sdl2)
target_compile_options(SDL2 PUBLIC -DSDL_DISABLE_ANALYZE_MACROS)

set(SDL_MIXER_SHARED OFF CACHE BOOL "")
add_subdirectory(third_party/sdl_mixer)

set(BUILD_SHARED OFF CACHE BOOL "")
set(BUILD_DEMOS OFF CACHE BOOL "")
add_subdirectory(third_party/chipmunk2d)
unset(BUILD_SHARED)
unset(BUILD_DEMOS)

set(GLM_TEST_ENABLE OFF CACHE BOOL "")
add_subdirectory(third_party/glm)

add_subdirectory(third_party/stb)

set(SDL2_LIBRARY SDL2)
set(SDL2_INCLUDE_DIR SDL2)
add_subdirectory(third_party/soloud/contrib)
unset(SDL2_LIBRARY)
unset(SDL2_INCLUDE_DIR)
target_include_directories(
    soloud PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/third_party/soloud/include>
)

set_target_properties(
    bgfx bimg bx chipmunk_static
    PROPERTIES POSITION_INDEPENDENT_CODE ON
)

add_subdirectory(src)

cmrc_add_resource_library(
    kaacore_embedded_assets

    shaders/binary/default_glsl_fragment_shader.bin
    shaders/binary/default_glsl_vertex_shader.bin
    shaders/binary/default_hlsl_d3d9_fragment_shader.bin
    shaders/binary/default_hlsl_d3d9_vertex_shader.bin
    shaders/binary/default_hlsl_d3d11_fragment_shader.bin
    shaders/binary/default_hlsl_d3d11_vertex_shader.bin

    embedded_resources/font_munro/munro.ttf
)

set_target_properties(
    kaacore_embedded_assets
    PROPERTIES POSITION_INDEPENDENT_CODE ON
)

target_link_libraries(kaacore PUBLIC kaacore_embedded_assets)

if (KAACORE_BUILD_DEMOS)
    add_subdirectory(demos)
endif()
